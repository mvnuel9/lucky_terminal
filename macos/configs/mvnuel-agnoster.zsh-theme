# vim:ft=zsh ts=2 sw=2 sts=2
#
# Original agnoster's Theme - https://gist.github.com/3712874
# Mvnuel : Agnoster + palette personnalisée (hex truecolor).
# Fond segments : #251810 / #3a2a20 — aligné sur Mvnuel.itermcolors / palette commune.
# Copie alignée sur ../../linux/configs/mvnuel-agnoster.zsh-theme (install : macos/install.sh).

CURRENT_BG='NONE'

() {
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  SEGMENT_SEPARATOR=$'\ue0b0'
}

# $1 bg  $2 fg  $3 texte
prompt_segment() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

prompt_end() {
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    echo -n "%{%k%}"
  fi
  echo -n "%{%f%}"
  CURRENT_BG=''
}

# Nom d’utilisateur uniquement (palette Mvnuel — root / user)
prompt_context() {
  if [[ $UID -eq 0 ]]; then
    prompt_segment "#3a2a20" "#d44040" "%n"
  else
    prompt_segment "#3a2a20" "#e0855a" "%n"
  fi
}

prompt_git() {
  (( $+commands[git] )) || return
  local PL_BRANCH_CHAR
  () {
    local LC_ALL="" LC_CTYPE="en_US.UTF-8"
    PL_BRANCH_CHAR=$'\ue0a0'
  }
  local ref dirty mode repo_path
  repo_path=$(git rev-parse --git-dir 2>/dev/null)

  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    dirty=$(parse_git_dirty)
    ref=$(git symbolic-ref HEAD 2> /dev/null) || ref="➦ $(git rev-parse --short HEAD 2> /dev/null)"
    if [[ -n $dirty ]]; then
      prompt_segment "#d4a060" "#AA2727"
    else
      prompt_segment "#d4a060" "#AA2727"
    fi

    if [[ -e "${repo_path}/BISECT_LOG" ]]; then
      mode=" <B>"
    elif [[ -e "${repo_path}/MERGE_HEAD" ]]; then
      mode=" >M<"
    elif [[ -e "${repo_path}/rebase" || -e "${repo_path}/rebase-apply" || -e "${repo_path}/rebase-merge" || -e "${repo_path}/../.dotest" ]]; then
      mode=" >R>"
    fi

    setopt promptsubst
    autoload -Uz vcs_info

    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' get-revision true
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' stagedstr '+'
    zstyle ':vcs_info:*' unstagedstr '-'
    zstyle ':vcs_info:*' formats ' %u%c'
    zstyle ':vcs_info:*' actionformats ' %u%c'
    vcs_info
    echo -n "${ref/refs\/heads\//$PL_BRANCH_CHAR }${vcs_info_msg_0_%% }${mode}"
  fi
}

prompt_bzr() {
    (( $+commands[bzr] )) || return
    if (bzr status >/dev/null 2>&1); then
        status_mod=`bzr status | head -n1 | grep "modified" | wc -m`
        status_all=`bzr status | head -n1 | wc -m`
        revision=`bzr log | head -n2 | tail -n1 | sed 's/^revno: //'`
        if [[ $status_mod -gt 0 ]] ; then
            prompt_segment "#3a2a20" "#d44040"
            echo -n "bzr@"$revision "✚ "
        else
            if [[ $status_all -gt 0 ]] ; then
                prompt_segment "#3a2a20" "#e0a840"
                echo -n "bzr@"$revision

            else
                prompt_segment "#3a2a20" "#a8c870"
                echo -n "bzr@"$revision
            fi
        fi
    fi
}

prompt_hg() {
  (( $+commands[hg] )) || return
  local rev status
  if $(hg id >/dev/null 2>&1); then
    if $(hg prompt >/dev/null 2>&1); then
      if [[ $(hg prompt "{status|unknown}") = "?" ]]; then
        prompt_segment "#3a2a20" "#d44040"
        st='±'
      elif [[ -n $(hg prompt "{status|modified}") ]]; then
        prompt_segment "#3a2a20" "#e0a840"
        st='±'
      else
        prompt_segment "#3a2a20" "#a8c870"
      fi
      echo -n $(hg prompt "☿ {rev}@{branch}") $st
    else
      st=""
      rev=$(hg id -n 2>/dev/null | sed 's/[^-0-9]//g')
      branch=$(hg id -b 2>/dev/null)
      if `hg st | grep -q "^\?"`; then
        prompt_segment "#3a2a20" "#d44040"
        st='±'
      elif `hg st | grep -q "^[MA]"`; then
        prompt_segment "#3a2a20" "#e0a840"
        st='±'
      else
        prompt_segment "#3a2a20" "#a8c870"
      fi
      echo -n "☿ $rev@$branch" $st
    fi
  fi
}

prompt_dir() {
  # prompt_segment 008 010 $(basename `pwd`)
}

prompt_virtualenv() {
  if [[ -n "${CONDA_PROMPT_MODIFIER:-}" ]]; then
    prompt_segment "#251810" "#d4a060" ${CONDA_PROMPT_MODIFIER:1:-2}
    return
  fi
  [[ -n "$VIRTUAL_ENV" ]] || return
  local env_name="${VIRTUAL_ENV_PROMPT:-$(basename "$VIRTUAL_ENV")}"
  [[ -n "$env_name" ]] || return
  prompt_segment "#251810" "#d4a060" "$env_name"
}

prompt_status() {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{#d44040}%}✘"
  [[ $UID -eq 0 ]] && symbols+="%{%F{#e0a040}%}⚡"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{#60c8d0}%}⚙"

  [[ -n "$symbols" ]] && prompt_segment "#1a1210" "default" "$symbols"
}

prompt_head() {
  echo "\r               "
  echo "\r %{%F{#a8c870}%}[%64<..<%~%<<]"
}

build_prompt() {
  RETVAL=$?
  prompt_head
  prompt_status
  prompt_virtualenv
  prompt_context
  # prompt_dir
  prompt_git
  prompt_bzr
  prompt_hg
  prompt_end
}

PROMPT='%{%f%b%k%}$(build_prompt) '
